apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionName "4.1.1a"
    }
    sourceSets {
        lightningPlus.setRoot('src/LightningPlus')
        lightningLite.setRoot('src/LightningLite')
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-project.txt'
        }
    }
    productFlavors {
        lightningPlus {
            buildConfigField "boolean", "FULL_VERSION", "true"
            applicationId "acr.browser.lightning"
            versionCode 80
        }
        lightningLite {
            buildConfigField "boolean", "FULL_VERSION", "false"
            applicationId "acr.browser.barebones"
            versionCode 81
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:palette-v7:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'

    // Only Lightning Plus needs the proxy libraries
    lightningPlusCompile 'net.i2p.android:client:0.7'
    lightningPlusCompile(project(':libnetcipher'))

    provided 'javax.annotation:jsr250-api:1.0'
    // git submodule foreach git reset --hard
    // git submodule update --remote
}